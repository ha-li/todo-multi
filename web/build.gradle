import dsl.GoogleClosureMinifier

apply plugin: 'java'
apply plugin: 'groovy'
apply plugin: 'war'

//sourceCompatibility = 1.5

description = 'Good ole fashion lookie lookie'

repositories {
   mavenCentral()

   // an ivy repository configured to point to jquery host
   // use this to download jquery library, and the artifact
   // pattern is specified here, used in dependencies { ... }
   ivy {
      name 'JQuery'
      url 'http://code.jquery.com'
      layout 'pattern', {
         artifact '[module]-[revision](.[classifier]).[ext]'
      }
   }
}

configurations {
   jquery
   googleClosure
}

dependencies {
   compile localGroovy()
   compile gradleApi()
   compile 'org.codehaus.groovy:groovy-all:2.4.7'

   compile project (':repository')
   providedCompile 'javax.servlet:servlet-api:2.5'
   runtime 'javax.servlet:jstl:1.1.2'

   testCompile group: 'junit', name: 'junit', version: '4.11'

   googleClosure 'com.google.javascript:closure-compiler:v20130603'

   // add a dependency on the jquery library as per config above
   jquery group: 'jquery', name: 'jquery', version: '1.10.0', classifier: 'min', ext: 'js'
}

// a task to fetch jquery library from an external
// repository that is not maven central
task fetchExternalJs (type: Copy) {
   from configurations.jquery
   into "$webAppDir/js/ext"
}

// create sources jar in each sub project
task sourcesJar (type: Jar) {
   baseName = project.name
   classifier = 'sources'
   from sourceSets.main.allSource
}

///// add some extensions for minifying javascript code
ext {
   jsSourceDir = "$webAppDir/js/app"
   jsOutputDir = "$buildDir/js"
}

def jsSourceFiles = fileTree (jsSourceDir).include ('*.js')

task minifyJs (type: dsl.GoogleClosureMinifier) {
   inputFiles = jsSourceFiles
   outputFile = file ("$jsOutputDir/all-min.js")
}
///// end of minifying task and extension
